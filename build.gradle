plugins { id "com.jfrog.bintray" version "1.7.3" }
apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = 'org.xlbean'
archivesBaseName = 'xltemplating'
version = '0.0.6'

description = "xltemplating"

sourceCompatibility = 1.8
targetCompatibility = 1.8

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    failOnError = false
    destinationDir = file('docs')
    options.locale = 'en_US'
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

bintray {
    user = project.hasProperty('BINTRAY_USER') ? project.getProperty("BINTRAY_USER") : System.getenv('BINTRAY_USER')
    key = project.hasProperty('BINTRAY_API_KEY') ? project.getProperty("BINTRAY_API_KEY") : System.getenv('BINTRAY_API_KEY')

    pkg {
        repo = 'xlbean'
        name = 'xltemplating'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/aji3/xltemplating.git'
        publicDownloadNumbers = true
        version {
            name = project.version
            desc = project.archivesBaseName + ' ' + project.version
            released  = new Date()
            vcsTag = project.version
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
            gpg {
                sign = true
                passphrase = project.hasProperty('BINTRAY_GPG_KEY') ? project.getProperty('BINTRAY_GPG_KEY') : System.getenv('BINTRAY_GPG_KEY')
            }
        }
    }
    publications = ['mavenPublication']
    configurations = ['archives']
}

publishing {
    publications {
        mavenPublication(MavenPublication) { from components.java }
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
}

dependencies {
    compile group: 'com.mitchellbosecke', name: 'pebble', version:'2.3.0'
    compile group: 'org.codehaus.groovy', name: 'groovy-jsr223', version:'2.4.10'
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile group: 'ch.qos.logback', name: 'logback-classic', version:'1.2.2'
    compile group: 'ch.qos.logback', name: 'logback-core', version:'1.2.2'
    compile ('org.xlbean:xlscript:0.1.0')
    compile group: 'args4j', name: 'args4j', version: '2.33'
    testRuntime("org.junit.platform:junit-platform-launcher:1.3.2")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:5.3.2")
    testRuntime("org.junit.vintage:junit-vintage-engine:5.3.2")
    testRuntime 'org.hamcrest:hamcrest:2.1'
}
